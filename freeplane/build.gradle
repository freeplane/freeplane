
dependencies {
    compile project(':JOrtho_0.4_freeplane')

    compile 'commons-lang:commons-lang:2.6',
            'commons-io:commons-io:2.4',
            'commons-codec:commons-codec:1.7',
            'com.jgoodies:jgoodies-forms:1.6.0',
            'com.jgoodies:jgoodies-common:1.4.0',
            'com.github.insubstantial:flamingo:7.2.1',
            'com.github.insubstantial:substance:7.2.1',
            'com.github.insubstantial:substance-flamingo:7.2.1',
            'com.github.insubstantial:substance-swingx:7.2.1',
            'com.github.insubstantial:trident:7.2.1',
            'gnu-regexp:gnu-regexp:1.1.4'

    // from flatDir "localGitDepsRepository":
    compile ':idw-gpl-1.6.1'
    compile ':SimplyHTML-0.16.09'
}

jar {
    manifest {

        name = pluginid
        symbolicName = pluginid

        def deps = configurations.runtime.files.collect { "lib/${it.name}" }
        deps.add(0, "lib/freeplaneosgi-" + project.version + ".jar")
        // this is built from :freeplane_mac on a Mac!
        //deps.add(0, "lib/freeplanemac.jar")
        deps.add(0, "lib/freeplaneeditor-" + project.version + ".jar")
        deps.add(0, "lib/freeplaneviewer-" + project.version + ".jar")

        attributes 'Class-Path' : deps.join(', ')
        attributes 'Bundle-ClassPath': '., ' + deps.join(', ')
        // f*ck bnd! it filters things like "org.apache.commons.*" from
        // Export-Package:, that's why we bypass bnd for now!
        def exports = """\
                    com.inet.jortho,
                    org.freeplane.core.extension,
                    org.freeplane.core.io,
                    org.freeplane.core.io.xml,
                    org.freeplane.core.resources,
                    org.freeplane.core.resources.components,
                    org.freeplane.core.ui,
                    org.freeplane.core.ui.components,
                    org.freeplane.core.ui.components.calendar,
                    org.freeplane.core.ui.components.html,
                    org.freeplane.core.ui.ribbon,
                    org.freeplane.core.ui.ribbon.event,
                    org.freeplane.core.ui.ribbon.special,
                    org.freeplane.core.undo,
                    org.freeplane.core.util,
                    org.freeplane.core.util.collection,
                    org.freeplane.features.attribute,
                    org.freeplane.features.attribute.mindmapmode,
                    org.freeplane.features.clipboard,
                    org.freeplane.features.clipboard.mindmapmode,
                    org.freeplane.features.cloud,
                    org.freeplane.features.cloud.mindmapmode,
                    org.freeplane.features.edge,
                    org.freeplane.features.edge.mindmapmode,
                    org.freeplane.features.encrypt,
                    org.freeplane.features.encrypt.mindmapmode,
                    org.freeplane.features.export.mindmapmode,
                    org.freeplane.features.filter,
                    org.freeplane.features.filter.condition,
                    org.freeplane.features.format,
                    org.freeplane.features.help,
                    org.freeplane.features.icon,
                    org.freeplane.features.icon.factory,
                    org.freeplane.features.icon.mindmapmode,
                    org.freeplane.features.link,
                    org.freeplane.features.link.mindmapmode,
                    org.freeplane.features.map,
                    org.freeplane.features.map.filemode,
                    org.freeplane.features.map.mindmapmode,
                    org.freeplane.features.mapio,
                    org.freeplane.features.mapio.mindmapmode,
                    org.freeplane.features.mode,
                    org.freeplane.features.mode.browsemode,
                    org.freeplane.features.mode.filemode,
                    org.freeplane.features.mode.mindmapmode,
                    org.freeplane.features.nodelocation,
                    org.freeplane.features.nodelocation.mindmapmode,
                    org.freeplane.features.nodestyle,
                    org.freeplane.features.nodestyle.filemode,
                    org.freeplane.features.nodestyle.mindmapmode,
                    org.freeplane.features.note,
                    org.freeplane.features.note.mindmapmode,
                    org.freeplane.features.print,
                    org.freeplane.features.script,
                    org.freeplane.features.spellchecker.mindmapmode,
                    org.freeplane.features.styles,
                    org.freeplane.features.styles.mindmapmode,
                    org.freeplane.features.text,
                    org.freeplane.features.text.mindmapmode,
                    org.freeplane.features.time,
                    org.freeplane.features.ui,
                    org.freeplane.features.url,
                    org.freeplane.features.url.mindmapmode,
                    org.freeplane.main.addons,
                    org.freeplane.main.applet,
                    org.freeplane.main.application,
                    org.freeplane.main.application.protocols.freeplaneresource,
                    org.freeplane.main.browsemode,
                    org.freeplane.main.filemode,
                    org.freeplane.main.headlessmode,
                    org.freeplane.main.mindmapmode,
                    org.freeplane.main.mindmapmode.stylemode,
                    org.freeplane.main.osgi,
                    org.freeplane.n3.nanoxml,
                    org.freeplane.plugin.macos,
                    org.freeplane.view.swing.features,
                    org.freeplane.view.swing.features.filepreview,
                    org.freeplane.view.swing.features.nodehistory,
                    org.freeplane.view.swing.features.progress.mindmapmode,
                    org.freeplane.view.swing.features.time.mindmapmode,
                    org.freeplane.view.swing.map,
                    org.freeplane.view.swing.map.attribute,
                    org.freeplane.view.swing.map.cloud,
                    org.freeplane.view.swing.map.edge,
                    org.freeplane.view.swing.map.link,
                    org.freeplane.view.swing.map.mindmapmode,
                    org.freeplane.view.swing.ui,
                    org.freeplane.view.swing.ui.mindmapmode,
                    com.jgoodies.forms.builder,
                    com.jgoodies.forms.debug,
                    com.jgoodies.forms.factories,
                    com.jgoodies.forms.layout,
                    com.jgoodies.forms.util,
                    com.lightdev.app.shtm,
                    org.apache.commons.io,
                    org.apache.commons.io.comparator,
                    org.apache.commons.io.filefilter,
                    org.apache.commons.io.input,
                    org.apache.commons.io.monitor,
                    org.apache.commons.io.output,
                    org.apache.commons.lang,
                    org.apache.commons.lang.builder,
                    org.apache.commons.lang.exception,
                    org.apache.commons.lang.math,
                    org.apache.commons.lang.time,
                    org.pushingpixels.flamingo.api.common,
                    org.pushingpixels.flamingo.api.common.icon,
                    org.pushingpixels.flamingo.api.common.model,
                    org.pushingpixels.flamingo.api.common.popup,
                    org.pushingpixels.flamingo.api.ribbon,
                    org.pushingpixels.flamingo.api.ribbon.resize
                    """
        attributes 'Export-Package': exports.replaceAll("\\s+", "")

        instruction 'Bundle-Vendor', 'Freeplane Project'
        instruction 'Import-Package', 'org.osgi.framework,org.osgi.service.url;version="1.0.0"'
        // unfortunately bnd filters "org.apache.commons.*" so we use attributes to bypass bnd (see above)
        //instruction 'Export-Package', '*;-noimport:=true'
        //instruction 'Private-Package', '*'
        instruction 'Bundle-Activator', 'org.freeplane.main.osgi.Activator'
        instruction 'Bundle-RequiredExecutionEnvironment', 'J2SE-1.6'
    }
}

task copyOSGiJars(type: Copy) {
     from ("$buildDir/libs") {
          include("*.jar")
     }
     from ('lib') {
          include("*.jar")
          exclude('*-sources.jar')
     }
     into(globalBuild + '/core/' + pluginid + '/lib/')
}

task copyOSGiManifest(type: Copy) {
     from ("$buildDir/tmp/jar/") {
          include("MANIFEST.MF")
     }
     into(globalBuild + '/core/' + pluginid + '/META-INF/')
}

task viewerJar(type: Jar) {
    // use '= project.manifest' to avoid that an OSGi manifest is created!
    manifest = project.manifest {
        attributes("Manifest-Version": "1.0",
                   "Permissions": "all-permissions",
                   "Codebase": "*",
                   "Application-Library-Allowable-Codebase": "*",
                   "Application-Name": "Freeplane Viewer Applet")
    }

    baseName = 'freeplaneviewer'
    dependsOn classes
    from (sourceSets.main.output) {
        exclude('**/application/**')
        exclude('**/osgi/**')
        exclude('**/mindmapmode/**')
        exclude('**/filemode/**')
        exclude('**/ortho/**')
        exclude('**/core/resources/ui/**')
        exclude('translations/**')
    }
    from ('./viewer-resources') {
        exclude('**/appletMenu.mm')
    }
    includeEmptyDirs = false
}

task editorJar(type: Jar) {
    // use '= project.manifest' to avoid that an OSGi manifest is created!
    manifest = project.manifest {
        attributes("Manifest-Version": "1.0",
                   "Main-Class": "org.freeplane.main.application.FreeplaneMain")
    }
    baseName = 'freeplaneeditor'
    dependsOn classes
    from (sourceSets.main.output) {
				include("**/application/**")
				include("**/mindmapmode/**")
				include("**/ortho/**")
				include("**/filemode/**")
				include("**/core/resources/ui/**")
				include("translations/**")
				exclude("**/*.jar") // probably not necessary
    }
    from ('./resources') {
				exclude("**/preferences.mm")
				exclude("**/browsemodemenu.mm")
				exclude("**/mindmapmodemenu.mm")
				exclude("**/filemodemenu.mm")
				exclude("**/stylemodemenu.mm")
    }
    includeEmptyDirs = false
}

task osgiJar(type: Jar) {
    // use '= project.manifest' to avoid that an OSGi manifest is created!
    manifest = project.manifest {
        attributes("Manifest-Version": "1.0")
    }
    baseName = 'freeplaneosgi'
    dependsOn classes
    from (sourceSets.main.output) {
         include("**/osgi/**")         
    }
    includeEmptyDirs = false
}

task copyExternalResources(type: Copy) {
    from('external-resources') {
        include('**')
    }
    into(globalBuild + '/resources')
}

task copyDoc(type: Copy) {
    from('doc/') {
        include('**')
    }
    into(globalBuild + '/doc')
}

// This task calls out to ant to perform an xslt task since it is an easier
// dependency to satisfy than a seperate gradle plugin. We may wish to change
// this in the future
task build_ui_xml {
    doFirst {
        ant.xslt(in   : file('resources/xml/mindmapmoderibbon.xml'),
              style: file('resources/xslt/ribbons2menu.xsl'),
              out  : new File('resources/xml', 'mindmapmodemenu.xml')
        )
    }
}

jar.dependsOn viewerJar
jar.dependsOn editorJar
jar.dependsOn osgiJar
jar.dependsOn copyDeps
build.dependsOn copyOSGiJars
build.dependsOn copyOSGiManifest
build.dependsOn copyExternalResources
build.dependsOn copyDoc
build.dependsOn build_ui_xml

apply from: './format_translation.gradle'